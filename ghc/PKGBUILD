# Special note for devs looking to upgrade this package:
#   ghc places a unique hash for each library when it is built.
#   Libraries depend on versions specified by those hashes.
#   This implies that all libraries need to be rebuilt when ghc is rebuilt.

pkgname=ghc
pkgver=7.10.2
pkgrel=2
pkgdesc='The Glasgow Haskell Compiler'
arch=('i686' 'x86_64')
url='http://www.haskell.org/ghc/'
license=('custom')
depends=('perl' 'gmp>=5.0' 'gcc')
makedepends=('ghc' 'perl' 'libxslt' 'docbook-xsl' 'happy' 'alex')
checkdepends=('python2')
install='ghc.install'
options=('strip' 'staticlibs')

provides=('haskell-array=0.5.1.0'
          'haskell-base=4.8.1.0'
          'haskell-binary=0.7.5.0'
          'haskell-bin-package-db=0.0.0.0'
          'haskell-bytestring=0.10.6.0'
          'haskell-containers=0.5.6.2'
          'haskell-deepseq=1.4.1.1'
          'haskell-directory=1.2.2.0'
          'haskell-filepath=1.4.0.0'
          'haskell-ghc-prim=0.4.0.0'
          'haskell-haskeline=0.7.2.1'
          'haskell-hoopl=3.10.0.2'
          'haskell-hpc=0.6.0.2'
          'haskell-integer-gmp=1.0.0.0'
          'haskell-pretty=1.1.2.0'
          'haskell-process=1.2.3.0'
          'haskell-rts=1.0'
          'haskell-template-haskell=2.10.0.0'
          'haskell-terminfo=0.4.0.1'
          'haskell-time=1.5.0.1'
          'haskell-transformers=0.4.2.0'
          'haskell-unix=2.7.1.0'
          'haskell-xhtml=3000.2.1'
          'haskell-cabal=1.22.4.0')
replaces=('haskell-array<0.5.1.0'
          'haskell-base<4.8.1.0'
          'haskell-binary<0.7.5.0'
          'haskell-bin-package-db<0.0.0.0'
          'haskell-bytestring<0.10.6.0'
          'haskell-containers<0.5.6.2'
          'haskell-deepseq<1.4.1.1'
          'haskell-directory<1.2.2.0'
          'haskell-filepath<1.4.0.0'
          'haskell-ghc-prim<0.4.0.0'
          'haskell-haskeline<0.7.2.1'
          'haskell-hoopl<3.10.0.2'
          'haskell-hpc<0.6.0.2'
          'haskell-integer-gmp<1.0.0.0'
          'haskell-pretty<1.1.2.0'
          'haskell-process<1.2.3.0'
          'haskell-rts<1.0'
          'haskell-template-haskell<2.10.0.0'
          'haskell-terminfo<0.4.0.1'
          'haskell-time<1.5.0.1'
          'haskell-transformers<0.4.2.0'
          'haskell-unix<2.7.1.0'
          'haskell-xhtml<3000.2.1'
          'haskell-cabal<1.22.4.0')

source=("https://downloads.haskell.org/~ghc/${pkgver}/ghc-${pkgver}-src.tar.xz"
        "https://downloads.haskell.org/~ghc/${pkgver}/ghc-${pkgver}-testsuite.tar.xz"
        'build.mk' 'arch-gen-contents-index')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    cp "${srcdir}/build.mk" mk/build.mk

    # clean up some things that seem to have been left in by mistake
    ( cd testsuite/mk; rm -f ghc-config{,.hi,.o} )
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix=/usr
    make -j 5
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # python2 rename
    sed -e 's_#!/usr/bin/env python_&2_' -i testsuite/timeout/calibrate testsuite/timeout/timeout.py

    #make test
    make THREADS=9 test

    # zero unexpected failures on all tier 1 platforms - http://hackage.haskell.org/trac/ghc/ticket/5757
    # enable this when upstream has 0 test failures from a simple 'make test'
    #make fulltest
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR=${pkgdir} install

    # clean up the doc index, it's re-generated in ghc.install
    ( cd ${pkgdir}/usr/share/doc/ghc/html/libraries ;
    rm doc-index*.html frames.html index-frames.html index.html \
        ocean.css haddock-util.js \
        hslogo-16.png  minus.gif plus.gif synopsis.png
    )
    # install our own contents generator
    install -m755 ${srcdir}/arch-gen-contents-index \
        ${pkgdir}/usr/share/doc/ghc/html/libraries

    install -d ${pkgdir}/usr/share/licenses/ghc
    install -m644 LICENSE \
        ${pkgdir}/usr/share/licenses/ghc
}

sha256sums=('54cd73755b784d78e2f13d5eb161bfa38d3efee9e8a56f7eb6cd9f2d6e2615f5'
            '8b4885d376ca635935b49d4e36e2fa6f07164563ea496eac5fffa0ac926ae962'
            'f6f08c0fd1098396beba515db06d6c5f3aac633b56e2d35cbcefaf76272ee9af'
            'f800cd0de93ae9926bb65c6324d6ce3e1ee814dae9c70c7a7bce28e5deb8245c')
